# $n11
      $id      $idq      $dec      $int 
    $bool      $str         (         ) 
        +         -         *         / 
        <         =         >        <= 
       >=       and       not        or 
     cond      else        if       let 
   define   display   newline      set! 

#
        S -> PROG #1
     PROG -> CALCS #2 |
             DEFS #3 |
             DEFS CALCS #4
        E -> $id #5 |
             $int #6 |
             $dec #7 |
             AREX #8 |
             IF #9 |
             EASYLET #10 |
             CPROC #11
     AREX -> HAREX E ) #12
    HAREX -> ( AROP #13 |
             HAREX E #14
     AROP -> + #15 |
             - #16 |
             * #17 |
             / #18
       IF -> ( if BOOL E E ) #19
  EASYLET -> HEASYL E ) #20
   HEASYL -> ( let ( ) #21 |
             HEASYL INTER #22
    CPROC -> HCPROC ) #23
   HCPROC -> ( $id #24 |
             HCPROC E #25
      STR -> $str #26 |
             SCOND #27
    SCOND -> ( cond SBRANCHES ) #28
SBRANCHES -> SELSE #29 |
             SCLAUS SBRANCHES #30
   SCLAUS -> ( BOOL STR ) #31
    SELSE -> ( else STR ) #32
     BOOL -> $bool #33 |
             $idq #34 |
             REL #35 |
             AND #36 |
             ( not BOOL ) #37 |
             CPRED #38
    CPRED -> HCPRED ) #39
   HCPRED -> ( $idq #40 |
             HCPRED ARG #41
      ARG -> E #42 |
             BOOL #43
      REL -> ( = E E ) #44 |
             ( <= E E ) #45
      AND -> HAND BOOL ) #46
     HAND -> ( and #47 |
             HAND BOOL #48
      SET -> HSET E ) #49
     HSET -> ( set! $id #50
  DISPSET -> ( display E ) #51 |
             ( display BOOL ) #52 |
             ( display STR ) #53 |
             ( newline ) #54 |
             SET #55
    INTER -> DISPSET #56 |
             E #57
    CALCS -> CALC #58 |
             CALCS CALC #59
     CALC -> E #60 |
             BOOL #61 |
             STR #62 |
             DISPSET #63
     DEFS -> DEF #64 |
             DEFS DEF #65
      DEF -> PRED #66 |
             VAR #67 |
             PROC #68
     PRED -> HPRED BOOL ) #69
    HPRED -> PDPAR ) #70
    PDPAR -> ( define ( $idq #71 |
             PDPAR $idq #72 |
             PDPAR $id #73
      VAR -> VARDCL E ) #74
   VARDCL -> ( define $id #75
     PROC -> HPROC BLOCK ) #76 |
             HPROC E ) #77
    HPROC -> PCPAR ) #78 |
             HPROC INTER #79
    PCPAR -> ( define ( $id #80 |
             PCPAR $id #81
    BLOCK -> HBLOCK E ) #82
   HBLOCK -> BLVAR ) #83 |
             HBLOCK INTER #84
    BLVAR -> ( let ( LOCDEF #85 |
             BLVAR LOCDEF #86
   LOCDEF -> ( $id E ) #87

